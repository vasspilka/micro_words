defmodule MicroWords.Rulesets.Basic do
  # TODO: use MicroWords.Ruleset, extends: MicroWords.Rulesets.Basic
  use MicroWords.Ruleset,
    dimensions: [100, 100],
    action_names: [:forge_artefact, :plant_arteface, :boost_artefact, :impair_artefact]

  # somehow make this be generated by action names
  @type action_type :: :forge_artefact | :plant_arteface | :boost_artefact | :impair_artefact

  @artefact_energy_cost 40

  @impl true
  def initial_energy(%Explorer{}) do
    200
  end

  @impl true
  def apply(%Explorer{} = o, %ExplorerActionTaken{action: %{type: :forge_artefact} = act}) do
    artefact = %Artefact{
      id: act.artefact_id,
      originator: o.id,
      world: o.world,
      energy: @artefact_energy_cost,
      content: act.data.content
    }

    %Explorer{
      o
      | energy: o.energy - @artefact_energy_cost,
        artefacts: Map.put(o.artefacts, act.artefact_id, artefact)
    }
  end

  @impl true
  def apply(%Explorer{} = o, %ExplorerAffected{
        action: %{type: :plant_artefact, progress: :passed} = act
      }) do
    %Explorer{o | artefacts: Map.delete(o.artefacts, act.data.artefact.id)}
  end

  @impl true
  def apply(%Location{} = o, %LocationAffected{action: %{type: :plant_artefact} = act}) do
    %Location{o | artefact: act.data.artefact}
  end

  # def apply(%Explorer{} = o, %Action{type: :boost_artefact}) do
  #   %Explorer{o | energy: o.energy - 20}
  # end

  @impl true
  def reaction(%Journey{}, %ExplorerActionTaken{action: %{type: :plant_artefact} = act}) do
    %AffectLocation{location_id: act.location_id, action: act}
  end

  @impl true
  def execute(%Location{artefact: nil}, %AffectLocation{action: %{type: :plant_artefact} = act}) do
    %LocationAffected{id: act.location_id, action: %{act | progress: :passed}}
  end

  @impl true
  def execute(%Location{}, %AffectLocation{action: %{type: :plant_artefact}}) do
    {:error, :location_not_empty}
  end

  # maybe we can use norm to validate if an action can be taken
  # would be also usefull if we want to do property testing with
  # actions
  @impl true
  def valid_action?(%Explorer{energy: energy}, %Action{type: :forge_artefact}) do
    energy >= @artefact_energy_cost
  end

  @impl true
  def valid_action?(%Explorer{}, %Action{type: :plant_artefact}) do
    true
  end

  @impl true
  def valid_action?(%Explorer{energy: energy}, %Action{type: :boost_artefact}) do
    energy > 20
  end

  # BoostArtefact: Would give energy to artefact
  # ImpairArtefact: Would harm artefact to destroy it

  @impl true
  def build_action(explorer, :forge_artefact, %{content: content}) do
    %Action{
      type: :forge_artefact,
      artefact_id: UUID.uuid4(),
      explorer_id: explorer.id,
      location_id: Location.id_from_attrs(explorer),
      ruleset: __MODULE__,
      data: %{content: content}
    }
  end

  @impl true
  def build_action(explorer, :plant_artefact, %{artefact_id: artefact_id}) do
    %Action{
      type: :plant_artefact,
      artefact_id: artefact_id,
      explorer_id: explorer.id,
      location_id: Location.id_from_attrs(explorer),
      ruleset: __MODULE__,
      data: %{artefact: explorer.artefacts[artefact_id]}
    }
  end
end
